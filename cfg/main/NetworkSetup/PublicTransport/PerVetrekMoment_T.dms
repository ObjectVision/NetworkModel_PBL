Template PerVetrekMoment_T
{
	parameter<uint32>   inTime;
	parameter<string>   inTime_string;
	///
	parameter<min>      Max_V_Time := max(/ModelParameters/Advanced/OV_Voortransport_Typen/MaxTime[min]);
	unit<uint8>         ParetoAttr := Classifications/FinalParetoAttr;
	attribute<bool>     Within_VW_Window_SelectionCriterium (PublicTransport_Prep/PT) := PublicTransport_Prep/PT/From_Time_rel <= inTime + convert(Max_V_Time, s_f)[Time] + ModelParameters/Advanced/MaxWachttijdThuis;
	
	unit<uint64> Within_VW_Window := select(Within_VW_Window_SelectionCriterium)
	, Descr = "Opties binnen voortransport + wachten thuis window"
	{
		attribute<ct>                Price                       := collect_by_cond(., Within_VW_Window_SelectionCriterium, PublicTransport_Prep/PT/Price);
		attribute<ct>                Price_augm                  := collect_by_cond(., Within_VW_Window_SelectionCriterium, PublicTransport_Prep/PT/Price_augm);
		attribute<s>                 Traveltime                  := collect_by_cond(., Within_VW_Window_SelectionCriterium, PublicTransport_Prep/PT/Traveltime);
		attribute<string>            ModeUsed                    := collect_by_cond(., Within_VW_Window_SelectionCriterium, PublicTransport_Prep/PT/ModeUsed);
		
		attribute<c_Time_Places>     c_FromTime_Place_rel        := collect_by_cond(., Within_VW_Window_SelectionCriterium, PublicTransport_Prep/PT/c_FromTime_Place_rel);
		attribute<c_Time_Places>     c_ToTime_Place_rel          := collect_by_cond(., Within_VW_Window_SelectionCriterium, PublicTransport_Prep/PT/c_ToTime_Place_rel);
		attribute<Places>            From_Place_rel              := value(c_FromTime_Place_rel % uint64(#Places), Places);
		attribute<Time>              From_Time_rel               := value(c_FromTime_Place_rel / uint64(#Places), Time);

		attribute<dam>               TravelDist_Bus              := collect_by_cond(., Within_VW_Window_SelectionCriterium, PublicTransport_Prep/PT/TravelDist_Bus);
		attribute<dam>               Traveldist_Metro            := collect_by_cond(., Within_VW_Window_SelectionCriterium, PublicTransport_Prep/PT/Traveldist_Metro);
		attribute<dam>               Traveldist_Tram             := collect_by_cond(., Within_VW_Window_SelectionCriterium, PublicTransport_Prep/PT/Traveldist_Tram);
		attribute<dam>               Traveldist_Rail             := collect_by_cond(., Within_VW_Window_SelectionCriterium, PublicTransport_Prep/PT/Traveldist_Rail);
		attribute<dam>               Traveldist_Ferry            := collect_by_cond(., Within_VW_Window_SelectionCriterium, PublicTransport_Prep/PT/Traveldist_Ferry);
	}

	unit<uint64> Add_V_to_PT   := join_equal_values_uint64(V/To_Place_rel, Within_VW_Window/From_Place_rel)
	{
		attribute<V>                                           V_rel                            := first_rel; //debug
		attribute<Within_VW_Window>                            PT_rel                           := second_rel; //debug
		attribute<Places>                                      Place_rel                        := X_rel; //debug

		attribute<Time>                                        V_From_Time_rel_org              := const(inTime[Time],V)[V_rel], Descr = "Departure time from org, excluding waiting at first stop.";
		attribute<Time>                                        V_To_Time_rel_org                := V_From_Time_rel_org + V_time[Time];
		
		attribute<Time>                                        V_From_Time_rel                  := V_From_Time_rel_org + Wachttijd_Time_rel, Descr = "Departure time from origin, excluding waiting at first stop.";
		attribute<Places>                                      V_From_Place_rel                 := V/From_Place_rel[V_rel];
		attribute<c_Time_Places>                               V_c_fromTime_Place_rel           := combine_data(c_Time_Places, V_From_Time_rel, V_From_Place_rel);
		
		attribute<Time>                                        PT_From_Time_rel                 := Within_VW_Window/From_Time_rel[PT_rel];
		attribute<Places>                                      PT_From_Place_rel                := Within_VW_Window/From_Place_rel[PT_rel];
		attribute<Stops>                                       PT_From_Stop_rel                 := Places/Stop_rel[PT_From_Place_rel];
		attribute<c_Time_Places>                               PT_c_fromTime_Place_rel          := Within_VW_Window/c_fromTime_Place_rel[PT_rel];
		attribute<c_Time_Places>                               PT_c_toTime_Place_rel            := Within_VW_Window/c_toTime_Place_rel[PT_rel];
		
		attribute<c_Time_Stops>                                PT_c_fromTime_Stop_rel           := combine_data(c_Time_Stops, PT_From_Time_rel, PT_From_Stop_rel);
		attribute</SourceData/Infrastructuur/GTFS/RelevantSelection/ScheduledLinks> IsPT_departureEvent_ActualPT0     := rlookup(PT_c_fromTime_Stop_rel, /SourceData/Infrastructuur/GTFS/RelevantSelection/ScheduledLinks/c_fromTime_Stop_rel), Descr = "the departure event should be a depature event, and not waiting or already a transfer. To make sure, this check is built in.";
		attribute<bool>                                        IsPT_departureEvent_ActualPT                           := IsDefined(IsPT_departureEvent_ActualPT0);
		
		attribute<Time>                                        StartWacht_Time_rel              := V_To_Time_rel_org;
		attribute<Time>                                        EindWacht_Time_rel               := value(PT_c_fromTime_Place_rel / uint64(#Places), Time);
		attribute<bool>                                        IsWachttijdValid                 := StartWacht_Time_rel < EindWacht_Time_rel;
		
		attribute<s>                                           V_time_wo_Penalty                := V/impedance[V_rel];
		attribute<Modes>                                       V_mode_rel                       := V/Mode_rel[V_rel];
		attribute<VN_ModeClasses/Uq>                           V_ModeClass_rel                  := V/ModeClass_rel[V_rel];
		attribute<ct>                                          V_price                          := V/Price[V_rel];
		attribute<s>                                           V_time                           := V_mode_rel == Modes/v/Cycling 
																									? V_time_wo_Penalty + convert(ModelParameters/Advanced/FietsVoortransport_Penalty, s) 
																									: V_time_wo_Penalty;
		
		attribute<Time>                                        Wachttijd_Time_rel               := MakeDefined(sub_or_null(EindWacht_Time_rel, StartWacht_Time_rel), 0[Time]);
		attribute<s>                                           W_time                           := Wachttijd_Time_rel[s];
		
		attribute<c_Time_Places>                               c_fromTime_Place_rel             := V_c_fromTime_Place_rel;
		attribute<c_Time_Places>                               c_toTime_Place_rel               := PT_c_toTime_Place_rel;

		attribute<ct>                                          Price                            := ModelParameters/Advanced/IncludeCyclingCostsInPrice &&  V_mode_rel == Modes/v/Cycling
																									? Within_VW_Window/Price[PT_rel] + V_price
																									: Within_VW_Window/Price[PT_rel];
		attribute<ct>                                          Price_augm                       := Within_VW_Window/Price_augm[PT_rel] + V_price;
		attribute<s>                                           PT_time                          := Within_VW_Window/Traveltime[PT_rel];
		attribute<string>                                      ModeUsed                         := VN_ModeClasses/Uq/name[V_ModeClass_rel] + '_' + Within_VW_Window/ModeUsed[PT_rel];
		// attribute<Toegestane_Ketens>                           Toegestane_Keten_rel             := Within_VW_Window/Toegestane_Keten_rel[PT_rel];
		attribute<s>                                           Traveltime                       := (ModelParameters/Advanced/WachttijdThuisMeetellen * W_time) + V_time + PT_time;
		
		attribute<dam>                                         TravelDist_Bus                   := Within_VW_Window/TravelDist_Bus[PT_rel];
		attribute<dam>                                         Traveldist_Metro                 := Within_VW_Window/Traveldist_Metro[PT_rel];
		attribute<dam>                                         Traveldist_Tram                  := Within_VW_Window/Traveldist_Tram[PT_rel];
		attribute<dam>                                         Traveldist_Rail                  := Within_VW_Window/Traveldist_Rail[PT_rel];
		attribute<dam>                                         Traveldist_Ferry                 := Within_VW_Window/Traveldist_Ferry[PT_rel];
		
		attribute<bool>                                        IsVTimeNotTooLong                := switch(
																										case(V_mode_rel == Modes/v/Walking, V_time <= convert(ModelParameters/MaxWalkingTime_Org2Stops, s))
																										, case(V_mode_rel == Modes/v/Cycling, V_time <= convert(ModelParameters/MaxCyclingTime_Org2Stops, s))
																										, FALSE
																									);
		attribute<bool>                                        IsValid                          := Wachttijd_Time_rel <= ModelParameters/Advanced/MaxWachttijdThuis && IsWachttijdValid && IsVTimeNotTooLong && IsPT_departureEvent_ActualPT;
		
		unit<uint64> Result := select(IsValid)
		{
			attribute<c_Time_Places>               c_fromTime_Place_rel             := collect_by_cond(., ../IsValid, ../c_fromTime_Place_rel);
			attribute<c_Time_Places>               c_toTime_Place_rel               := collect_by_cond(., ../IsValid, ../c_toTime_Place_rel);
			attribute<Places>                      To_Place_rel                     := value(c_ToTime_Place_rel % uint64(#Places), Places);
			
			attribute<s>                           W_time                           := collect_by_cond(., ../IsValid, ../W_time), Descr = "Waiting at home";
			attribute<s>                           V_time                           := collect_by_cond(., ../IsValid, ../V_time);
			attribute<s>                           PT_time                          := collect_by_cond(., ../IsValid, ../PT_time);
			
			attribute<ct>                          Price                            := collect_by_cond(., ../IsValid, ../Price);
			attribute<ct>                          Price_augm                       := collect_by_cond(., ../IsValid, ../Price_augm);
			attribute<s>                           Traveltime                       := collect_by_cond(., ../IsValid, ../Traveltime);
			attribute<string>                      ModeUsed                         := collect_by_cond(., ../IsValid, ../ModeUsed);

			attribute<dam>                         TravelDist_Bus                   := collect_by_cond(., ../IsValid, ../TravelDist_Bus);
			attribute<dam>                         Traveldist_Metro                 := collect_by_cond(., ../IsValid, ../Traveldist_Metro);
			attribute<dam>                         Traveldist_Tram                  := collect_by_cond(., ../IsValid, ../Traveldist_Tram);
			attribute<dam>                         Traveldist_Rail                  := collect_by_cond(., ../IsValid, ../Traveldist_Rail);
			attribute<dam>                         Traveldist_Ferry                 := collect_by_cond(., ../IsValid, ../Traveldist_Ferry);
		}
	}
	
	unit<uint64> Add_N_to_VPT   := join_equal_values_uint64(Add_V_to_PT/Result/To_Place_rel, N/From_Place_rel)
	{
		attribute<Add_V_to_PT/Result>                          PT_rel                           := first_rel; //debug
		attribute<N>                                           N_rel                            := second_rel; //debug
		attribute<Places>                                      Place_rel                        := X_rel; //debug
		
		attribute<c_Time_Places>                               PT_c_fromTime_Place_rel          := Add_V_to_PT/Result/c_fromTime_Place_rel[PT_rel];
		attribute<c_Time_Places>                               PT_c_toTime_Place_rel            := Add_V_to_PT/Result/c_toTime_Place_rel[PT_rel];
		attribute<Time>                                        PT_To_Time_rel                   := value(PT_c_toTime_Place_rel / uint64(#Places), Time);
		attribute<Time>                                        N_To_Time_rel                    := PT_To_Time_rel + N_time[Time];
		attribute<Places>                                      N_to_Place_rel                   := N/To_Place_rel[N_rel];
		attribute<s>                                           N_time                           := N/impedance[N_rel];
		attribute<Modes>                                       N_mode_rel                       := N/Mode_rel[N_rel];
		attribute<VN_ModeClasses/Uq>                           N_ModeClass_rel                  := N/ModeClass_rel[N_rel];
		attribute<ct>                                          N_price                          := N/Price[N_rel];
		
		attribute<c_Time_Places>                               c_fromTime_Place_rel             := PT_c_fromTime_Place_rel;
		attribute<c_Time_Places>                               c_toTime_Place_rel               := combine_data(c_Time_Places, N_To_Time_rel, N_to_Place_rel);
		
		attribute<ct>                                          Price                            := ModelParameters/Advanced/IncludeCyclingCostsInPrice &&  N_mode_rel == Modes/v/Cycling
																									? Add_V_to_PT/Result/Price[PT_rel] + N_price
																									: Add_V_to_PT/Result/Price[PT_rel];
		attribute<ct>                                          Price_augm                       := Add_V_to_PT/Result/Price_augm[PT_rel] + N_price;
		attribute<string>                                      ModeUsed                         := Add_V_to_PT/Result/ModeUsed[PT_rel] + '_' + VN_ModeClasses/Uq/name[N_ModeClass_rel];
		attribute<s>                                           Traveltime                       := Add_V_to_PT/Result/Traveltime[PT_rel] + N_time;
		attribute<s>                                           W_time                           := Add_V_to_PT/Result/W_time[PT_rel];
		attribute<s>                                           V_time                           := Add_V_to_PT/Result/V_time[PT_rel];
		attribute<s>                                           PT_time                          := Add_V_to_PT/Result/PT_time[PT_rel];

		attribute<dam>                                         TravelDist_Bus                   := Add_V_to_PT/Result/TravelDist_Bus[PT_rel];
		attribute<dam>                                         Traveldist_Metro                 := Add_V_to_PT/Result/Traveldist_Metro[PT_rel];
		attribute<dam>                                         Traveldist_Tram                  := Add_V_to_PT/Result/Traveldist_Tram[PT_rel];
		attribute<dam>                                         Traveldist_Rail                  := Add_V_to_PT/Result/Traveldist_Rail[PT_rel];
		attribute<dam>                                         Traveldist_Ferry                 := Add_V_to_PT/Result/Traveldist_Ferry[PT_rel];
		
		attribute<bool>                                        IsNTimeNotTooLong                := switch(
																										  case(N_mode_rel == Modes/v/Walking, N_time <= convert(ModelParameters/MaxWalkingTime_Stops2Dest, s))
																										, case(N_mode_rel == Modes/v/Cycling, N_time <= convert(ModelParameters/MaxCyclingTime_Stops2Dest, s))
																										, FALSE
																									);
		attribute<bool>                                        IsValid                          := Traveltime <= /ModelParameters/MaxTravelTime[s] && IsNTimeNotTooLong;
		
		unit<uint64> Result := select(IsValid)
		{
			attribute<c_Time_Places>               c_fromTime_Place_rel             := collect_by_cond(., ../IsValid, ../c_fromTime_Place_rel);
			attribute<c_Time_Places>               c_toTime_Place_rel               := collect_by_cond(., ../IsValid, ../c_toTime_Place_rel);
		  
			attribute<s>                           W_time                           := collect_by_cond(., ../IsValid, ../W_time), Descr = "Waiting at home";
			attribute<s>                           V_time                           := collect_by_cond(., ../IsValid, ../V_time);
			attribute<s>                           PT_time                          := collect_by_cond(., ../IsValid, ../PT_time);
			attribute<s>                           N_time                           := collect_by_cond(., ../IsValid, ../N_time);
			
			attribute<ct>                          Price                            := collect_by_cond(., ../IsValid, ../Price);
			attribute<ct>                          Price_augm                       := collect_by_cond(., ../IsValid, ../Price_augm);
			attribute<s>                           Traveltime                       := collect_by_cond(., ../IsValid, ../Traveltime);
			attribute<string>                      ModeUsed                         := collect_by_cond(., ../IsValid, ../ModeUsed);

			attribute<dam>                         TravelDist_Bus                   := collect_by_cond(., ../IsValid, ../TravelDist_Bus);
			attribute<dam>                         Traveldist_Metro                 := collect_by_cond(., ../IsValid, ../Traveldist_Metro);
			attribute<dam>                         Traveldist_Tram                  := collect_by_cond(., ../IsValid, ../Traveldist_Tram);
			attribute<dam>                         Traveldist_Rail                  := collect_by_cond(., ../IsValid, ../Traveldist_Rail);
			attribute<dam>                         Traveldist_Ferry                 := collect_by_cond(., ../IsValid, ../Traveldist_Ferry);
		}
	}
	
	unit<uint64> Union_with_DirectOD   := union_unit_uint64(Add_N_to_VPT/Result, Direct_OD)
	{
		attribute<c_Time_Places>               c_fromTime_Place_rel             := union_data(., Add_N_to_VPT/Result/c_FromTime_Place_rel,  combine_data(c_Time_Places, inTime[Time], Direct_OD/From_Place_rel)); 
		attribute<c_Time_Places>               c_toTime_Place_rel               := union_data(., Add_N_to_VPT/Result/c_toTime_Place_rel, combine_data(c_Time_Places, inTime[Time] + Direct_OD/Duration_seconds[Time], Direct_OD/To_Place_rel)); 
		
		unit<uint32> uq_c_FromTime_Place := unique_uint32(c_fromTime_Place_rel);
		unit<uint32> uq_c_ToTime_Place   := unique_uint32(c_toTime_Place_rel);

		attribute<uq_c_FromTime_Place>         uq_c_FromTime_Place_rel          := rlookup(c_fromTime_Place_rel, uq_c_FromTime_Place/Values);
		attribute<uq_c_ToTime_Place>           uq_c_ToTime_Place_rel            := rlookup(c_toTime_Place_rel, uq_c_ToTime_Place/Values);

		attribute<Time>                        From_Time_rel                    := value(c_FromTime_Place_rel / uint64(#Places), Time);
		attribute<Places>                      From_Place_rel                   := value(c_FromTime_Place_rel % uint64(#Places), Places);
		attribute<Time>                        To_Time_rel                      := value(c_ToTime_Place_rel / uint64(#Places), Time);
		attribute<Places>                      To_Place_rel                     := value(c_ToTime_Place_rel % uint64(#Places), Places);

		attribute<s>                           W_time                           := union_data(., Add_N_to_VPT/Result/W_time,  const(0w, Direct_OD, s));
		attribute<s>                           V_time                           := union_data(., Add_N_to_VPT/Result/V_time,  Direct_OD/duration_seconds);
		attribute<s>                           PT_time                          := union_data(., Add_N_to_VPT/Result/PT_time, const(0w, Direct_OD, s));
		attribute<s>                           N_time                           := union_data(., Add_N_to_VPT/Result/N_time,  const(0w, Direct_OD, s));
		
		attribute<ct>                          Price                            := union_data(., Add_N_to_VPT/Result/Price, const(0w, Direct_OD, ct));
		attribute<ct>                          Price_augm                       := union_data(., Add_N_to_VPT/Result/Price_augm, Direct_OD/Price_Augm);
		attribute<s>                           Traveltime                       := union_data(., Add_N_to_VPT/Result/Traveltime, Direct_OD/duration_seconds);
		attribute<string>                      ModeUsed                         := union_data(., Add_N_to_VPT/Result/ModeUsed, Direct_OD/ModeUsed);
		
		attribute<bool>                        IsDirectTravel                   := union_data(., const(FALSE, Add_N_to_VPT/Result), const(TRUE, Direct_OD));
		
		attribute<dam>                         TravelDist_Bus                   := union_data(., Add_N_to_VPT/Result/TravelDist_Bus, const(0w, Direct_OD, dam));
		attribute<dam>                         Traveldist_Metro                 := union_data(., Add_N_to_VPT/Result/Traveldist_Metro, const(0w, Direct_OD, dam));
		attribute<dam>                         Traveldist_Tram                  := union_data(., Add_N_to_VPT/Result/Traveldist_Tram, const(0w, Direct_OD, dam));
		attribute<dam>                         Traveldist_Rail                  := union_data(., Add_N_to_VPT/Result/Traveldist_Rail, const(0w, Direct_OD, dam));
		attribute<dam>                         Traveldist_Ferry                 := union_data(., Add_N_to_VPT/Result/Traveldist_Ferry, const(0w, Direct_OD, dam));

		///// ==== Step-wise condensation of result set ==== ////
 		unit<uint64>          OD_key_set                       := combine_unit_uint64(Union_with_DirectOD/uq_c_FromTime_Place, Places), Descr = "Step-wise condensation of result set: second condense the results based on time-place to place.";
		attribute<OD_key_set> OD_key                           := combine_data(OD_key_set, uq_c_FromTime_Place_rel, To_Place_rel);
		unit<uint64>          uq_OD_key                        := unique(OD_key);
		attribute<uq_OD_key>  uq_OD_key_rel                    := rlookup(OD_key, uq_OD_key/values); //attribuut om partitionering van price/traveltime etc te doen.
		
		attribute<.>          good_candidate_of_OD (uq_OD_key) := ='min_index('+first(ParetoAttr/Name)+', uq_OD_key_rel)', Descr = "do a pre-selection to limit the size of the pareto_candidate selection. Here, first select a point which is a good starting point from which to compare others.";
		attribute<.>          good_candidate_in_OD             := good_candidate_of_OD[uq_OD_key_rel], Descr = "";
		attribute<bool>       pe_compared_to_good_candidate    := =AsList(replace('@PA@ < good_candidate_in_OD->@PA@', '@PA@', ParetoAttr/Name), ' or '), Descr = "Option is Pareto efficient compared to good candidate.";
		attribute<bool>       is_also_good_candidate           := pe_compared_to_good_candidate OR (ID(.) == good_candidate_in_OD), Descr = "";
				
		unit<uint64> Valid := select(is_also_good_candidate)
		{
			attribute<uq_OD_key>  uq_OD_key_rel    := collect_by_cond(., ../uq_OD_key_rel);
			attribute<s>          TravelTime       := collect_by_cond(., ../TravelTime);
			attribute<Ct>         Price            := collect_by_cond(., ../Price);
			attribute<Ct>         Price_augm       := collect_by_cond(., ../Price_augm);
		}
		unit<uint64> pareto_vergelijker := join_equal_values_uint64(Valid/uq_OD_key_rel, Valid/uq_OD_key_rel)
		{
			attribute<bool>       is_less_or_equal := =AsList('Valid/'+ ParetoAttr/Name+'[./first_rel] <= Valid/'+ParetoAttr/Name+'[./second_rel]',  ' and ');
			attribute<bool>       is_equal         := =AsList('Valid/'+ ParetoAttr/Name+'[./first_rel] == Valid/'+ParetoAttr/Name+'[./second_rel]',  ' and ');
			attribute<bool>       is_less          := is_less_or_equal && (not(is_equal) || first_rel < second_rel);
		}
		attribute<bool>       Valid_pareto_condition   (Valid) := not(any(pareto_vergelijker/is_less, pareto_vergelijker/second_rel));
		attribute<bool>       pareto_condition                 := recollect_by_cond(is_also_good_candidate, Valid_pareto_condition, false);
	}
	
	unit<uint64> Result_AfterFirstCondensation := select(Union_with_DirectOD/pareto_condition)
	, Descr = "Set that still contains duplicate from places, departing at different times. So we need to condense it again."
	{
		attribute<ct>                                       Price                       := collect_by_cond(., Union_with_DirectOD/pareto_condition, Union_with_DirectOD/Price);
		attribute<ct>                                       Price_augm                  := collect_by_cond(., Union_with_DirectOD/pareto_condition, Union_with_DirectOD/Price_augm);
		attribute<s>                                        Traveltime                  := collect_by_cond(., Union_with_DirectOD/pareto_condition, Union_with_DirectOD/Traveltime);
		attribute<string>                                   ModeUsed                    := collect_by_cond(., Union_with_DirectOD/pareto_condition, Union_with_DirectOD/ModeUsed);
		attribute<s>                                        W_time                      := collect_by_cond(., Union_with_DirectOD/pareto_condition, Union_with_DirectOD/W_time), Descr = "Waiting at home";
		attribute<s>                                        V_time                      := collect_by_cond(., Union_with_DirectOD/pareto_condition, Union_with_DirectOD/V_time);
		attribute<s>                                        PT_time                     := collect_by_cond(., Union_with_DirectOD/pareto_condition, Union_with_DirectOD/PT_time);
		attribute<s>                                        N_time                      := collect_by_cond(., Union_with_DirectOD/pareto_condition, Union_with_DirectOD/N_time);
		
		attribute<dam>                                      TravelDist_Bus              := collect_by_cond(., Union_with_DirectOD/pareto_condition, Union_with_DirectOD/TravelDist_Bus);
		attribute<dam>                                      Traveldist_Metro            := collect_by_cond(., Union_with_DirectOD/pareto_condition, Union_with_DirectOD/Traveldist_Metro);
		attribute<dam>                                      Traveldist_Tram             := collect_by_cond(., Union_with_DirectOD/pareto_condition, Union_with_DirectOD/Traveldist_Tram);
		attribute<dam>                                      Traveldist_Rail             := collect_by_cond(., Union_with_DirectOD/pareto_condition, Union_with_DirectOD/Traveldist_Rail);
		attribute<dam>                                      Traveldist_Ferry            := collect_by_cond(., Union_with_DirectOD/pareto_condition, Union_with_DirectOD/Traveldist_Ferry);
		
		attribute<Places>                                   From_Place_rel              := collect_by_cond(., Union_with_DirectOD/pareto_condition, Union_with_DirectOD/From_Place_rel);
		attribute<Places>                                   To_Place_rel                := collect_by_cond(., Union_with_DirectOD/pareto_condition, Union_with_DirectOD/To_Place_rel);
		attribute<bool>                                     IsDirectTravel              := collect_by_cond(., Union_with_DirectOD/pareto_condition, Union_with_DirectOD/IsDirectTravel);
	
		///// ==== Step-wise condensation of result set ==== ////
 		unit<uint64>          OD_key_set                       := combine_unit_uint64(Places, Places), Descr = "Step-wise condensation of result set: third condense the results based on place to place.";
		attribute<OD_key_set> OD_key                           := combine_data(OD_key_set, From_Place_rel, To_Place_rel);
		unit<uint64>          uq_OD_key                        := unique(OD_key);
		attribute<uq_OD_key>  uq_OD_key_rel                    := rlookup(OD_key, uq_OD_key/values); //attribuut om partitionering van price/traveltime etc te doen.
		
		attribute<.>          good_candidate_of_OD (uq_OD_key) := ='min_index('+first(ParetoAttr/Name)+', uq_OD_key_rel)', Descr = "do a pre-selection to limit the size of the pareto_candidate selection. Here, first select a point which is a good starting point from which to compare others.";
		attribute<.>          good_candidate_in_OD             := good_candidate_of_OD[uq_OD_key_rel], Descr = "";
		attribute<bool>       pe_compared_to_good_candidate    := =AsList(replace('@PA@ < good_candidate_in_OD->@PA@', '@PA@', ParetoAttr/Name), ' or '), Descr = "Option is Pareto efficient compared to good candidate.";
		attribute<bool>       is_also_good_candidate           := pe_compared_to_good_candidate OR (ID(.) == good_candidate_in_OD), Descr = "";
				
		unit<uint64> Valid := select(is_also_good_candidate)
		{
			attribute<uq_OD_key>  uq_OD_key_rel    := collect_by_cond(., ../uq_OD_key_rel);
			attribute<s>          TravelTime       := collect_by_cond(., ../TravelTime);
			attribute<Ct>         Price            := collect_by_cond(., ../Price);
			attribute<Ct>         Price_augm       := collect_by_cond(., ../Price_augm);
		}
		unit<uint64> pareto_vergelijker := join_equal_values_uint64(Valid/uq_OD_key_rel, Valid/uq_OD_key_rel)
		{
			attribute<bool>       is_less_or_equal := =AsList('Valid/'+ ParetoAttr/Name+'[./first_rel] <= Valid/'+ParetoAttr/Name+'[./second_rel]',  ' and ');
			attribute<bool>       is_equal         := =AsList('Valid/'+ ParetoAttr/Name+'[./first_rel] == Valid/'+ParetoAttr/Name+'[./second_rel]',  ' and ');
			attribute<bool>       is_less          := is_less_or_equal && (not(is_equal) || first_rel < second_rel);
		}
		attribute<bool>       Valid_pareto_condition   (Valid) := not(any(pareto_vergelijker/is_less, pareto_vergelijker/second_rel));
		attribute<bool>       pareto_condition                 := recollect_by_cond(is_also_good_candidate, Valid_pareto_condition, false);
	}
	
	unit<uint64> Result_AfterSecondCondensation := select(Result_AfterFirstCondensation/pareto_condition)
	, Descr = "Final set"
	{
		attribute<ct>                                       Price                       := collect_by_cond(., Result_AfterFirstCondensation/pareto_condition, Result_AfterFirstCondensation/Price);
		attribute<ct>                                       Price_augm                  := collect_by_cond(., Result_AfterFirstCondensation/pareto_condition, Result_AfterFirstCondensation/Price_augm);
		attribute<s>                                        Traveltime                  := collect_by_cond(., Result_AfterFirstCondensation/pareto_condition, Result_AfterFirstCondensation/Traveltime);
		attribute<string>                                   ModeUsed                    := collect_by_cond(., Result_AfterFirstCondensation/pareto_condition, Result_AfterFirstCondensation/ModeUsed);
		attribute<s>                                        W_time                      := collect_by_cond(., Result_AfterFirstCondensation/pareto_condition, Result_AfterFirstCondensation/W_time), Descr = "Waiting at home";
		attribute<s>                                        V_time                      := collect_by_cond(., Result_AfterFirstCondensation/pareto_condition, Result_AfterFirstCondensation/V_time);
		attribute<s>                                        PT_time                     := collect_by_cond(., Result_AfterFirstCondensation/pareto_condition, Result_AfterFirstCondensation/PT_time);
		attribute<s>                                        N_time                      := collect_by_cond(., Result_AfterFirstCondensation/pareto_condition, Result_AfterFirstCondensation/N_time);
		
		attribute<dam>                                      TravelDist_Bus              := collect_by_cond(., Result_AfterFirstCondensation/pareto_condition, Result_AfterFirstCondensation/TravelDist_Bus);
		attribute<dam>                                      Traveldist_Metro            := collect_by_cond(., Result_AfterFirstCondensation/pareto_condition, Result_AfterFirstCondensation/Traveldist_Metro);
		attribute<dam>                                      Traveldist_Tram             := collect_by_cond(., Result_AfterFirstCondensation/pareto_condition, Result_AfterFirstCondensation/Traveldist_Tram);
		attribute<dam>                                      Traveldist_Rail             := collect_by_cond(., Result_AfterFirstCondensation/pareto_condition, Result_AfterFirstCondensation/Traveldist_Rail);
		attribute<dam>                                      Traveldist_Ferry            := collect_by_cond(., Result_AfterFirstCondensation/pareto_condition, Result_AfterFirstCondensation/Traveldist_Ferry);
		
		attribute<Places>                                   From_Place_rel              := collect_by_cond(., Result_AfterFirstCondensation/pareto_condition, Result_AfterFirstCondensation/From_Place_rel);
		attribute<Places>                                   To_Place_rel                := collect_by_cond(., Result_AfterFirstCondensation/pareto_condition, Result_AfterFirstCondensation/To_Place_rel);
		attribute<bool>                                     IsDirectTravel              := collect_by_cond(., Result_AfterFirstCondensation/pareto_condition, Result_AfterFirstCondensation/IsDirectTravel);

		attribute<string>                                   OrgName                     := Places/label[From_Place_rel];
		attribute<string>                                   DestName                    := Places/label[To_Place_rel];
		attribute<string>                                   OrgDestName                 := OrgName + '_' + DestName;
	
		attribute<bool>                                     IsDirectWithPrice := IsDirectTravel && price > 0[ct]; //zou allemaal false moeten zijn.
		attribute<bool>                                     IsOVWithoutPrice  := NOT(IsDirectTravel) && price == 0[ct] && TravelDist_Ferry == 0[dam]; //zou allemaal false moeten zijn.In Amsterdam komen dit voor vanwege gratis ferry.
	}
	
	container CreateExports
	{
		unit<uint64>       domain                                        := Result_AfterSecondCondensation, IntegrityCheck = "NOT(Result_AfterSecondCondensation/IsDirectWithPrice) && NOT(Result_AfterSecondCondensation/IsOVWithoutPrice)";
		attribute<string>  OrgName                              (domain) := domain/OrgName;
		attribute<string>  DestName                             (domain) := domain/DestName;
		attribute<string>  OrgDestName                          (domain) := OrgName + '_' + DestName;
		// attribute<float32> nr_items_of_interest_bereikbaar_decayed (org) := domain/nr_items_of_interest_bereikbaar_decayed;
		attribute<string>  Traveltime                           (domain) := string(convert(domain/Traveltime, min_f)); 
		attribute<string>  Price                                (domain) := string(convert(domain/Price, euro)); 
		attribute<string>  Price_augm                           (domain) := string(convert(domain/Price_augm, euro)); 
		attribute<string>  ModeUsed                             (domain) := domain/ModeUsed; 
		attribute<string>  TravelDist_Bus                       (domain) := string(convert(domain/TravelDist_Bus, m)); 
		attribute<string>  Traveldist_Metro                     (domain) := string(convert(domain/Traveldist_Metro, m));  
		attribute<string>  Traveldist_Tram                      (domain) := string(convert(domain/Traveldist_Tram, m)); 
		attribute<string>  Traveldist_Rail                      (domain) := string(convert(domain/Traveldist_Rail, m)); 
		attribute<string>  Traveldist_Ferry                     (domain) := string(convert(domain/Traveldist_Ferry, m));  
		attribute<string>  W_time                               (domain) := string(convert(domain/W_time, min_f));  
		attribute<string>  V_time                               (domain) := string(convert(domain/V_time, min_f));  
		attribute<string>  PT_time                              (domain) := string(convert(domain/PT_time, min_f));  
		attribute<string>  N_time                               (domain) := string(convert(domain/N_time, min_f));  

		container LongFormat
		{
			attribute<string>  BodyLines_fullOD (domain) := domain/OrgName + ';' + domain/DestName + ';' + Traveltime + ';' + Price + ';' + Price_augm + ';' + ModeUsed + ';' + TravelDist_Bus + ';' + Traveldist_Metro + ';' + Traveldist_Tram + ';' + Traveldist_Rail + ';' + Traveldist_Ferry + ';' + W_time + ';' + V_time + ';' + PT_time + ';' + N_time;
			parameter<string>  File_fullOD               := 
				'OrgName;DestName;Traveltime_'+inTime_str_short+
				';Price_'+inTime_str_short+
				';Price_Augm_'+inTime_str_short+
				';ModeUsed_'+inTime_str_short+
				';TravelDist_Bus_'+inTime_str_short+
				';TravelDist_Metro_'+inTime_str_short+
				';TravelDist_Tram_'+inTime_str_short+
				';TravelDist_Rail_'+inTime_str_short+
				';TravelDist_Ferry_'+inTime_str_short+
				';WaitingTime_'+inTime_str_short+
				';VoortransTime_'+inTime_str_short+
				';PTTime_'+inTime_str_short+
				';NatransTime_'+inTime_str_short+'\n'+
				AsList(BodyLines_fullOD, '\n')
			,	StorageName = "=fileNamePrefix+'tt'+fileNameSuffix +'.csv'", StorageType = "str";
		
			attribute<string>  BodyLines_woDistance (domain) := domain/OrgName + ';' + domain/DestName + ';' + Traveltime + ';' + Price + ';' + Price_augm + ';' + ModeUsed + ';' + W_time + ';' + V_time + ';' + PT_time + ';' + N_time;
			parameter<string>  File_woDistance               :=
				'OrgName;DestName;Traveltime_'+inTime_str_short+
				';Price_'+inTime_str_short+
				';Price_Augm_'+inTime_str_short+
				';ModeUsed_'+inTime_str_short+
				';WaitingTime_'+inTime_str_short+
				';VoortransTime_'+inTime_str_short+
				';PTTime_'+inTime_str_short+
				';NatransTime_'+inTime_str_short+'\n'+
				AsList(BodyLines_woDistance, '\n')
			,	StorageName = "=fileNamePrefix+'tt'+fileNameSuffix +'.csv'", StorageType = "str";
			
			parameter<string>  File              := =ModelParameters/Export_AfgelegdeAfstand ? 'File_fullOD' : 'File_woDistance';
		
		
			// attribute<string>  BodyLines_decay     (org) := org/name + ';' + string(nr_items_of_interest_bereikbaar_decayed);
			// parameter<string>  File_decay                := 'Org;'+ModelParameters/Advanced/items_of_interest+'_'+inTime_string+'\n'++AsList(BodyLines_decay, '\n')
																// , StorageName = "=fileNamePrefix+ModelParameters/Advanced/items_of_interest+'_decayed_'+fileNameSuffix +'.csv'", StorageType = "str";
			
			container Results_Traveltime
			{
				parameter<string>  Generate := ='File';
			}
			// container Results_Decay
			// {
				// parameter<string>  Generate := ='File_decay';
			// }
			
			container Results_Traveltime_Fence := PhaseContainer(Results_Traveltime, 'Results in '+NetworkSetup/Impl/OrgBlockDomain/name[BlockDomain_id]+' are finished calculating'); //
			// container Results_Decay_Fence      := PhaseContainer(Results_Decay, 'Results in region '+regio_name_if_enkel_sub+' are finished calculating'); //
		}
		
		parameter<string> inTime_str_short := rjoin(inTime_string, ModelParameters/Advanced/MeasureMoments/Name , ModelParameters/Advanced/MeasureMoments/TemplatableTextShrt);
		
		parameter<string> fileNamePrefix := ModelParameters/Advanced/fileNamePrefix;
		parameter<string> fileNameSuffix :=  
			'_' + ModelParameters/Analysis_Date + 
			'_' + inTime_str_short + 
			'_ORG-'    + ModelParameters/Orgset + '-' + NetworkSetup/Impl/OrgBlockDomain/name[BlockDomain_id] + 
			'_DEST-'   + ModelParameters/Advanced/DestSet_string +
			'_V-'   + V_typen +
			'_N-'   + N_typen +
			'_Trsf-' + string(ModelParameters/Advanced/Max_overstappen) +
			'_MinCrit-' + AsList(Classifications/FinalParetoAttr/FileNameElement, '_') +
			'_MaxOV-' + string(ModelParameters/MaxOVTime) + 'min'
			;
		
		parameter<string> Direct_str := ModelParameters/Advanced/AllowDirectCyclingOverWalking ? 'CC' : 'WW';
		parameter<string> V_typen := Direct_str+','+AsList(ModelParameters/Advanced/OV_Voortransport_Typen/ExportName, ',');
		parameter<string> N_typen := AsList(ModelParameters/Advanced/OV_Natransport_Typen/ExportName, ',');
	}
}
