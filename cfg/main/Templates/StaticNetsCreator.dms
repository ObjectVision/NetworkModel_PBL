Template StaticNetsCreator : using = "geometries"
{
	unit<uint32> inStops;
	unit<uint32> inLinks;
	
	unit<uint32> Routes := /SourceData/Infrastructuur/GTFS/LoadFeeds/Routes;
	unit<uint32> Stops := inStops
	{
		attribute<Time>       FirstEvent            :  = min(ScheduledSpaceTimeEvents/TimeRef, ScheduledSpaceTimeEvents/StopRef);
		attribute<Time>       LastEvent             :  = max(ScheduledSpaceTimeEvents/TimeRef, ScheduledSpaceTimeEvents/StopRef);
		attribute<uint32>     TotalScheduledEvents  :  = pcount(ScheduledSpaceTimeEvents/StopRef);
		attribute<AllPlaces>  PlaceRef              :  = rlookup(geometry, AllPlaces/Values);
	}
		
	unit<uint32> AllPlaces       := allLinks/Places;
	unit<uint32> AllTimes        := allLinks/UniqueTimeEvents;
	
	unit<uint32> ScheduledLinks  := inLinks 
	{
		attribute<ScheduledSpaceTimeEvents> from_sst   := rlookup(point(depTime, fromStop, upoint), ScheduledSpaceTimeEvents/Values);
		attribute<ScheduledSpaceTimeEvents> to_sst     := rlookup(point(arrTime, toStop, upoint), ScheduledSpaceTimeEvents/Values);
		attribute<s_f>                      Duration   := depTime <= arrTime 
															? float32(arrTime - depTime)[s_f]
															: float32((arrTime + #Time) - depTime)[s_f];
		attribute<string>                   Label  := 'Scheduled: ' + Time/Label[depTime] + ' @ ' + Stops/Name[fromStop] + ' to ' + Time/Label[arrTime] + ' @ ' + Stops/Name[toStop];
		
		unit<uint32> findUniqueSpaceTimeEvents := union_unit(ScheduledLinks, ScheduledLinks)
		{
			attribute<upoint> timesOfStop  := union_data(., point(ScheduledLinks/depTime, ScheduledLinks/fromStop), point(ScheduledLinks/arrTime, ScheduledLinks/toStop));
			attribute<Time>   TimeRef      := pointCol(timesOfStop);
		}
	}
	
	unit<uint32> ScheduledTimeEvents      := unique(ScheduledLinks/findUniqueSpaceTimeEvents/TimeRef);
	
	unit<uint32> ScheduledSpaceTimeEvents := unique(ScheduledLinks/findUniqueSpaceTimeEvents/TimesOfStop) 
	{
		attribute<Stops>    StopRef         := pointRow(Values)[Stops];
		attribute<Time>     TimeRef         := pointCol(Values)[Time];
		attribute<string>   Label           := Stops/Name[StopRef] + ' @ ' + Time/Label[TimeRef];
		attribute<.>        FirstMoment     := min_index(TimeRef, StopRef)[StopRef];
		attribute<.>        NextMoment      := StopRef[id(.)+1] = StopRef ? id(.) + 1 : FirstMoment;
		attribute<bool>     points_unsorted := Values != sort(Values); // check to verify Values are orderd ascendingly (documented as such in online documentation)
		attribute<uint32>   cumu_events_Stop:= cumulate(const(1,.,uint32), StopRef) - 1;
		attribute<upoint>   unique_id       := point(cumu_events_Stop, StopRef, upoint);
		
		attribute<uint32>   aantal_tijden_per_stop := pcount(StopRef)[StopRef];
	}
	
	container CreateTransfersSet 
	{
		unit<uint32> TransferFromStops := select_with_attr_by_org_rel(inStops, inStops/nTransfers > 0)
		{
			attribute<rdc>       geometry       := inStops/geometry[org_rel];
			attribute<uint32>    nTransfers     := inStops/nTransfers[org_rel];
			attribute<string>    Label          := inStops/Label[org_rel];
			attribute<StopsGrid> GridId         := inStops/GridId[org_rel];
			attribute<uint32>    Grid1D         := inStops/Grid1D[org_rel];
			attribute<uint32>    cumu_in_grid   := inStops/cumu_in_grid[org_rel];
			attribute<upoint>    unique_id      := inStops/unique_id[org_rel];
			attribute<uint32>    first_transfer := cumulate(nTransfers) - nTransfers;
			attribute<uint32>    nEvents        := pcount(ScheduledSpaceTimeEvents/StopRef)[org_rel];
			attribute<ModesList/List_selectie> Mode      := inStops/mode[org_rel];
		}
		
		unit<spoint> StopsGrid := inStops/StopsGrid;
		
		unit<uint32> Directions: NrOfRows = 5 
		{
			attribute<string> Label    : ['Top', 'Right', 'Bottom', 'Left', 'Centre'];
			attribute<int16>  row      : [0,1,0,-1,0];
			attribute<int16>  col      : [1,0,-1,0,0];
			attribute<spoint> geometry := point(row,col, spoint);
		}
		
		unit<uint32> TransferPaths := range(uint32, 0, sum(TransferFromStops/nTransfers))
		{
			attribute<TransferFromStops>       first_from_stop    := rlookup(id(.), TransferFromStops/first_transfer);
			attribute<TransferFromStops>       from_trans_StopRef := cumulate(uint32(IsDefined(first_from_stop))) - 1;
			attribute<inStops>                 from_StopRef       := TransferFromStops/org_rel[from_trans_StopRef];
			attribute<ModesList/List_selectie> from_Mode          := TransferFromStops/Mode[from_trans_StopRef];
			attribute<ModesList/List_selectie> to_Mode            := inStops/Mode[to_StopRef];
			
			attribute<bool>               NeedsTranferTimePenalty := ModesList/List_selectie/IsGelijkvloers[from_Mode] == 1b && ModesList/List_selectie/IsGelijkvloers[to_Mode] == 1b //van: bus, tram, walk    to bus, tram, walk  --> niks
																		? False
																		: ModesList/List_selectie/IsGelijkvloers[from_Mode] == 1b && ModesList/List_selectie/IsGelijkvloers[to_Mode] == 0b //van: bus, tram, walk to metro, rail, ferry --> penalty
																			? TRUE
																			: ModesList/List_selectie/IsGelijkvloers[from_Mode] == 0b && ModesList/List_selectie/IsGelijkvloers[to_Mode] == 0b //van: metro, rail, ferry to metro, rail, ferry --> penalty
																				? TRUE
																				: ModesList/List_selectie/IsGelijkvloers[from_Mode] == 0b && ModesList/List_selectie/IsGelijkvloers[to_Mode] == 1b //van: metro, rail, ferry to bus, tram, walk --> penalty
																					? TRUE
																					: (0/0)[bool];

			attribute<uint32>             nEvents            := pcount(ScheduledSpaceTimeEvents/StopRef)[from_StopRef];
			attribute<uint32>             first_trans_event  := cumulate(nEvents) - nEvents;
			attribute<upoint>             from_unique_id     := inStops/unique_id[from_stopRef];
			attribute<StopsGrid>          from_GridId        := inStops/GridId[from_StopRef];
			attribute<uint32>             nTop               := StopsGrid/nTop[from_GridId];
			attribute<uint32>             nRight             := StopsGrid/nRight[from_GridId];
			attribute<uint32>             nBottom            := StopsGrid/nBottom[from_GridId];
			attribute<uint32>             nLeft              := StopsGrid/nLeft[from_GridId];
			attribute<uint32>             first_trans        := TransferFromStops/first_transfer[from_trans_StopRef];
			attribute<Directions>         quad               := switch( //top, right, bottom, left, middle
																	 case(nTop > 0   	&& nTop    > (id(.) - first_trans), uint32(0)) 
																	,case(nRight > 0 	&& nRight  > (id(.) - (first_trans + nTop)), uint32(1))
																	,case(nBottom > 0 	&& nBottom > (id(.) - (first_trans + nRight + nTop)), uint32(2))
																	,case(nLeft > 0 	&& nLeft   > (id(.) - (first_trans + nBottom + nRight + nTop)), uint32(3)) 
																	,4
																)[Directions];
			attribute<StopsGrid>          to_GridId          := (spoint(from_GridId)+Directions/geometry[quad])[StopsGrid];
			attribute<uint32>             to_Grid1d          := StopsGrid/uniId[to_GridId];
			attribute<upoint>             to_unique_id       := point(seq2, to_Grid1d, upoint);
			attribute<inStops>            to_StopRef         := rlookup(to_unique_id, inStops/unique_id);
				
			attribute<uint32>             seq1               := id(.) 
																	- switch(
																		case(quad = 0, first_trans)
																		, case(quad = 1, first_trans + nTop)
																		, case(quad = 2, first_trans + nTop + nRight)
																		, case(quad = 3, first_trans + nTop + nRight + nBottom)
																		, first_trans + nTop + nRight + nBottom + nLeft
																	);
			attribute<uint32>             seq2               := quad = 4 && seq1 >= inStops/cumu_in_grid[from_StopRef] ? seq1 + 1 : seq1;
			
			attribute<string>             Label              := 'Transfer from ' + inStops/Label[from_StopRef] + ' to ' + inStops/Label[to_StopRef];
			attribute<rdc>                geometry (arc)     := points2sequence(GenLines/Points,GenLines/Seq,GenLines/Ord);
			attribute<m>                  Distance           := arc_length(geometry, m);
			attribute<s_f>                minTransferTime    := Distance / ModelParameters/Advanced/TransferEffectiveSpeed;
			
			unit<uint32> GenLines:= union_unit(TransferPaths, TransferPaths)
			{
				attribute<rdc>              Points := union_data(., inStops/geometry[from_StopRef], inStops/geometry[to_StopRef]);
				attribute<TransferPaths>    Seq    := union_data(., id(TransferPaths), id(TransferPaths));
				attribute<uint32>           Ord    := union_data(., const(0,TransferPaths,uint32), const(1,TransferPaths,uint32));
			}
		}
		unit<uint32> TransfersByStart := range(uint32, 0, sum(TransferPaths/nEvents))
		{
			attribute<TransferPaths>            first_trans_path        := rlookup(id(.), TransferPaths/first_trans_event);
			attribute<TransferPaths>            PathRef                 := cumulate(uint32(IsDefined(first_trans_path))) - 1;
			attribute<rdc>                      geometry (arc)          := TransferPaths/geometry[PathRef];
			attribute<Stops>                    fromStop                := TransferPaths/from_StopRef[PathRef];
			attribute<Stops>                    toStop                  := TransferPaths/to_StopRef[PathRef];
			attribute<bool>                     NeedsTranferTimePenalty := TransferPaths/NeedsTranferTimePenalty[PathRef];
			attribute<uint32>                   cumu_events             := cumulate(const(1,.,uint32), PathRef) - 1;
			attribute<ScheduledSpaceTimeEvents> SSTE_Ref                := rlookup(point(cumu_events, fromStop, upoint), ScheduledSpaceTimeEvents/unique_id);
			attribute<Time>                     fromTime                := ScheduledSpaceTimeEvents/TimeRef[SSTE_Ref];
			attribute<s_f>                      Duration                := NeedsTranferTimePenalty ? TransferPaths/minTransferTime[PathRef] + convert(ModelParameters/OngelijkvloersPenalty, s_f) : TransferPaths/minTransferTime[PathRef];
			attribute<Time>                     toTime                  := fromTime + Duration[Time] < #Time ? fromTime + Duration[Time] : fromTime + Duration[Time] -  #Time;
			attribute<string>                   Label                   := 'Transfer: ' + Time/Label[fromTime] + ' @ ' + Stops/Name[fromStop] + ' to ' + Time/Label[toTime] + ' @ ' + Stops/Name[toStop], DialogType = "LabelText";
		}
	}
	
	unit<uint32> allLinks := union_unit(ScheduledLinks, CreateTransfersSet/TransfersByStart)
	{
		attribute<rdc>                     geometry (arc)  := union_data(., ScheduledLinks/geometry, CreateTransfersSet/TransfersByStart/geometry); 
		attribute<Time>                    fromTime        := union_data(., ScheduledLinks/depTime, CreateTransfersSet/TransfersByStart/fromTime); 
		attribute<Time>                    toTime          := union_data(., ScheduledLinks/arrTime, CreateTransfersSet/TransfersByStart/toTime); 
		attribute<s_f>                     Duration        := union_data(., ScheduledLinks/Duration, CreateTransfersSet/TransfersByStart/Duration); 
		attribute<string>                  Label           := union_data(., ScheduledLinks/Label, CreateTransfersSet/TransfersByStart/Label), DialogType = "LabelText";
		attribute<uint32>                  TripRef         := union_data(., ScheduledLinks/TripRef, const(0/0,CreateTransfersSet/TransfersByStart));
		attribute<Routes>                  Route_rel       := union_data(., ScheduledLinks/Route_rel, const((0/0)[Routes],CreateTransfersSet/TransfersByStart));
		attribute<string>                  RouteName       := union_data(., ScheduledLinks/RouteName, const('Walking',CreateTransfersSet/TransfersByStart));
		attribute<string>                  RouteName_short := union_data(., ScheduledLinks/RouteName_short, const('Walking',CreateTransfersSet/TransfersByStart));
		attribute<Agencies>                Agency_rel      := union_data(., ScheduledLinks/Agency_rel, const(0/0,CreateTransfersSet/TransfersByStart));
		attribute<ModesList/List_selectie> Mode            := union_data(., ScheduledLinks/Mode, const(ModesList/List_selectie/V/Walking, CreateTransfersSet/TransfersByStart, ModesList/List_selectie));
		attribute<LinkTypes>               LinkType        := union_data(., const(LinkTypes/Values/Scheduled,ScheduledLinks,LinkTypes), const(LinkTypes/Values/Transfer,CreateTransfersSet/TransfersByStart,LinkTypes));
		attribute<rdc>                     fromPoint       := first_point(geometry);
		attribute<rdc>                     toPoint         := last_point(geometry);
		attribute<Places>                  FromPlace       := rlookup(fromPoint, Places/Values);
		attribute<string>                  FromPlace_name  := Places/Stop_name[FromPlace];
		attribute<Places>                  ToPlace         := rlookup(toPoint, Places/Values);
		attribute<string>                  ToPlace_name    := Places/Stop_name[ToPlace];
		
		unit<uint32> doubledLinks := union_unit(allLinks, allLinks)
		{
			attribute<Time>     Moment         := union_data(., fromTime, toTime);
			attribute<rdc>      Coords         := union_data(., fromPoint, toPoint);
			attribute<Stops>    StopRef        := rlookup(Coords, Stops/geometry);
			attribute<Places>   Place          := rlookup(Coords, Places/Values);
			attribute<upoint>   MomentxPlace   := point(Moment, Place, upoint);
			attribute<upoint>   MomentxStop    := point(Moment, StopRef, upoint);
		}
		
		unit<uint32> Places := unique(doubledLinks/Coords)
		{
			attribute<Stops>   StopRef     := rlookup(Values, Stops/geometry);
			attribute<string>  Stop_name   := Stops/Name[StopRef];
			attribute<rdc>     geometry    := values;
		}
		
		unit<uint32> UniqueTimeEvents   := unique(doubledLinks/Moment);
		unit<uint32> UniqueMomentXStop  := unique(doubledLinks/MomentxStop)
		{
			attribute<Stops>    StopRef            := pointRow(Values)[Stops];
			attribute<Time>     TimeRef            := pointCol(Values)[Time];
			attribute<string>   Label              := Stops/Name[StopRef] + ' @ ' + Time/Label[TimeRef];
			attribute<.>        FirstMoment        := min_index(TimeRef, StopRef)[StopRef];
			attribute<.>        NextMoment         := StopRef[id(.)+1] = StopRef ? id(.) + 1 : FirstMoment;
			attribute<bool>     points_unsorted    := Values != sort(Values); // check to verify Values are orderd ascendingly (documented as such in online documentation)
			
			unit<uint32>  MakeWaitingLines := union_unit(UniqueMomentXStop, UniqueMomentXStop)
			{
				attribute<rdc>                      geometry := union_data(., Stops/geometry[StopRef], Stops/geometry[StopRef]);
				attribute<UniqueMomentXStop>        Seq      := union_data(., id(UniqueMomentXStop), id(UniqueMomentXStop));
				attribute<uint32>                   Ord      := union_data(., const(0,UniqueMomentXStop,uint32), const(1,UniqueMomentXStop,uint32));
			}
		}
		unit<uint32> WaitingAtStop := UniqueMomentXStop 
		{
			attribute<rdc>          geometry (arc) := points2sequence(UniqueMomentXStop/MakeWaitingLines/geometry, UniqueMomentXStop/MakeWaitingLines/Seq, UniqueMomentXStop/MakeWaitingLines/Ord);
			attribute<Stops>        FromStop       := StopRef;
			attribute<Stops>        ToStop         := StopRef[NextMoment];
			attribute<AllPlaces>    FromPlace      := Stops/PlaceRef[FromStop];
			attribute<string>       FromPlace_name := Stops/name[FromStop];
			attribute<string>       ToPlace_name   := Stops/name[ToStop];
			attribute<AllPlaces>    ToPlace        := Stops/PlaceRef[ToStop];
			attribute<rdc>          FromPoint      := Stops/geometry[FromStop];
			attribute<rdc>          ToPoint        := Stops/geometry[ToStop];
			attribute<Time>         FromTime       := TimeRef;
			attribute<Time>         ToTime         := TimeRef[NextMoment];
			attribute<s_f>          Duration       := FromTime <= ToTime ? float32(ToTime - FromTime)[s_f] : float32((ToTime + #Time) - FromTime)[s_f]; // duration in seconds
			attribute<string>       Label          := 'Waiting at stop: ' + Stops/Name[FromStop] + ' from ' + Time/Label[FromTime] + ' to ' + Time/Label[ToTime];
		}
		unit<uint32> NodeSet:= unique(doubledLinks/MomentXPlace), DialogData = "Place", DialogType = "map"
		{
			attribute<AllPlaces> PlaceRef  := pointRow(Values);
			attribute<Time>      TimeRef   := pointCol(Values);
			attribute<rdc>       Place     := AllPlaces/Values[PlaceRef];
		}
	}

	unit<uint32> Static_Net := union_unit(allLinks, allLinks/WaitingAtStop)
	{
		attribute<rdc>                     geometry (arc)          := union_data(., allLinks/geometry, allLinks/WaitingAtStop/geometry);
		attribute<AllPlaces>               FromPlace               := union_data(., allLinks/FromPlace, allLinks/WaitingAtStop/FromPlace);
		attribute<string>                  FromPlace_name          := union_data(., allLinks/FromPlace_name, allLinks/WaitingAtStop/FromPlace_name);
		attribute<string>                  ToPlace_name            := union_data(., allLinks/ToPlace_name, allLinks/WaitingAtStop/ToPlace_name);
		attribute<AllPlaces>               ToPlace                 := union_data(., allLinks/ToPlace, allLinks/WaitingAtStop/ToPlace);
		attribute<rdc>                     FromPoint               := union_data(., allLinks/FromPoint, allLinks/WaitingAtStop/FromPoint);
		attribute<rdc>                     ToPoint                 := union_data(., allLinks/ToPoint, allLinks/WaitingAtStop/ToPoint);
		attribute<string>                  Label                   := union_data(., allLinks/Label, allLinks/WaitingAtStop/Label);
		attribute<Time>                    FromTime                := union_data(., allLinks/fromTime, allLinks/WaitingAtStop/fromTime);
		attribute<Time>                    ToTime                  := union_data(., allLinks/toTime, allLinks/WaitingAtStop/toTime);
		attribute<s_f>                     Duration                := union_data(., allLinks/Duration, allLinks/WaitingAtStop/Duration);
		attribute<LinkTypes>               LinkType                := union_data(., allLinks/LinkType, const(LinkTypes/Values/Waiting_at_Stop,allLinks/WaitingAtStop,LinkTypes));
		attribute<NodeSet>                 F1                      := rlookup(point(FromTime, FromPlace), NodeSet/Values);
		attribute<NodeSet>                 F2                      := rlookup(point(ToTime, ToPlace), NodeSet/Values);
		attribute<km>                      Length                  := arc_length(geometry, m)[km];
		attribute<ModesList/List_selectie> Mode                    := union_data(., allLinks/Mode, const(ModesList/List_selectie/V/Waiting, allLinks/WaitingAtStop, ModesList/List_selectie));
		attribute<uint32>                  TripRef                 := union_data(., allLinks/TripRef, const(0/0, allLinks/WaitingAtStop));
		attribute<Agencies>                Agency_rel              := union_data(., allLinks/Agency_rel, const(0/0, allLinks/WaitingAtStop));
		attribute<Routes>                  Route_rel               := union_data(., allLinks/Route_rel, const((0/0)[Routes], allLinks/WaitingAtStop));
		attribute<string>                  RouteName               := union_data(., allLinks/RouteName, const('Waiting', allLinks/WaitingAtStop));
		attribute<string>                  RouteName_short         := union_data(., allLinks/RouteName_short, const('Waiting', allLinks/WaitingAtStop));

		attribute<uint32>                  LinkTypeSum (LinkTypes) := pcount(LinkType);
		unit<uint32>                       NodeSet                 := allLinks/NodeSet;
		unit<uint32>                       NetCheck                := connected_parts(F1, F2);
	}
}
