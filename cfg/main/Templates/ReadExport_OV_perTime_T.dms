Template ReadExport_OV_perTime_T
{
	parameter<string> Export_CombinationTypes;
	parameter<string> Org_str;
	parameter<string> Dest_str;
	///
	
	parameter<bool> IsRegionMerged := Org_str == 'All';
	
	container PerTime := 
		for_each_ne(
			Advanced/MeasureMoments/name
			, 'ReadCSV_T('+quote(Advanced/MeasureMoments/name)+')'
		);
		
	Template ReadCSV_T
	{ 
		parameter<string> Time;	
		///
		parameter<string> inTime_str_short := rjoin(Time, ModelParameters/Advanced/MeasureMoments/Name , ModelParameters/Advanced/MeasureMoments/TemplatableTextShrt);

		parameter<string> fileNamePrefix := IsRegionMerged ? '%LocalDataProjDir%/Output/' : ModelParameters/Advanced/fileNamePrefix;
		
		parameter<string> ORG := IsRegionMerged ? '_ORG-' + ModelParameters/Orgset : '_ORG-' + ModelParameters/Orgset + '-' + Org_str; 
		
		parameter<string> fileNameSuffix :=  
			'_' + ModelParameters/Analysis_Date + 
			'_' + inTime_str_short + 
			ORG + 
			'_DEST-'   + ModelParameters/Advanced/DestSet_string +
			'_'+ Export_CombinationTypes +
			'_Trsf-' + string(ModelParameters/Advanced/Max_overstappen) +
			'_MinCrit-' + AsList(Classifications/FinalParetoAttr/FileNameElement, '_') +
			'_MaxOV-' + string(ModelParameters/MaxOVTime) + 'min'
			;
		
		parameter<string> filename       := fileNamePrefix+'tt'+fileNameSuffix +'.csv';
		
		unit<uint64>      domain         := range(uint64, 0[uint64], count_rows);
		parameter<string> fieldseparator := ';';
		parameter<string> filedata       :  StorageType   = "str",  StorageName = "=filename",  StorageReadOnly = "true";
		parameter<uint64> count_rows     := (strcount(filedata, fieldseparator) / strcount(headerline, fieldseparator))[uint64] - 1[uint64];
		parameter<string> headerline     := readLines(filedata, void, 0);
		
		unit<uint32> field := Range(uint32, 0, strcount(headerline, fieldseparator) + 1)
		{
			attribute<string> name := ReadArray(headerline , field, string, 0);
		}
			
		attribute<string> bodylines (domain) := readLines(filedata, domain, headerline/ReadPos);

		container data := 
			for_each_nedv(
				field/name
				,'ReadElems(
					BodyLines
					,string
					,'+ MakeDefined(field/name[sub_or_null(id(field),1)] + '/ReadPos','const(0, domain)')+'
					, 17
				)'
				,domain
				,string
			)
		{
			attribute<min_f>               Traveltime              (domain) := ='Traveltime_'+inTime_str_short+'[float32] * 1[min_f]';
			attribute<string>              ModeUsed                (domain) := ='ModeUsed_'+inTime_str_short;
			attribute<string>              OrgDestName             (domain) := OrgName + '_' + DestName;
			attribute<string>              Price                   (domain) := ='Price_'+inTime_str_short;
			attribute<string>              WaitingTime             (domain) := ='WaitingTime_'+inTime_str_short;
			attribute<string>              VoortransTime           (domain) := ='VoortransTime_'+inTime_str_short;
			attribute<string>              PTTime                  (domain) := ='PTTime_'+inTime_str_short;
			attribute<string>              NatransTime             (domain) := ='NatransTime_'+inTime_str_short;
		}
	}
}
