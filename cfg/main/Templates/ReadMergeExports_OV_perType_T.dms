Template ReadMergeExports_OV_perType_T
{
	parameter<string> Export_CombinationTypes;
	parameter<string> Dest;
	//
	
	container Read_Regions := 
		for_each_ne(
			Impl/OrgBlockDomain/name 
			, 'Templates/ReadExport_OV_perTime_T('+quote(Export_CombinationTypes)+','+quote(Impl/OrgBlockDomain/name)+','+quote(Dest)+')'
		);
		
	container Merge_Regions :=
		for_each_ne(
			 Advanced/MeasureMoments/name
			, 'Templates/Merge_Regions_OV_T('+quote(Export_CombinationTypes)+','+quote( Advanced/MeasureMoments/name)+','+quote(Dest)+')'
		)
	{
		parameter<string>   OUTPUT_Merge_Regions  := =AsList(Advanced/MeasureMoments/Name+'/LongFormat/File_fullOD',' + ');
	}
	
	container Read_Times  := Templates/ReadExport_OV_perTime_T(Export_CombinationTypes,'All',Dest);
	container Merge_Times := Merge_Times_OV_T(Export_CombinationTypes,Dest);
	
	
	
	
	Template Merge_Times_OV_T
	{
		parameter<string> Export_CombinationTypes;
		parameter<string> Dest_str;
		//
		
		unit<uint32> Merged := ='union_unit('+AsItemList('Read_Times/PerTime/'+Advanced/MeasureMoments/Name+'/domain')+')'
		{
			attribute<string>  OrgName    := ='union_data(.,'+AsItemList('Read_Times/PerTime/'+Advanced/MeasureMoments/Name+'/data/OrgName')+')';
			attribute<string>  DestName   := ='union_data(.,'+AsItemList('Read_Times/PerTime/'+Advanced/MeasureMoments/Name+'/data/DestName')+')';
			attribute<string>  DepTime    := ='union_data(.,'+AsList('const('+quote(Advanced/MeasureMoments/Name)+',Read_Times/PerTime/'+Advanced/MeasureMoments/Name+'/domain)',',')+')';
			attribute<min_f>   Traveltime := ='union_data(.,'+AsItemList('Read_Times/PerTime/'+Advanced/MeasureMoments/Name+'/data/Traveltime')+')';
			attribute<string>  ModeUsed   := ='union_data(.,'+AsItemList('Read_Times/PerTime/'+Advanced/MeasureMoments/Name+'/data/ModeUsed')+')';
			attribute<string>  Price      := ='union_data(.,'+AsItemList('Read_Times/PerTime/'+Advanced/MeasureMoments/Name+'/data/Price')+')';
			attribute<string>  Label      := OrgName +'_'+DestName, DialogType = "LabelText";
		}
		
		container LongFormat
		{
			attribute<string>  BodyLines_fullOD      (Merged) := Merged/OrgName + ';' + Merged/DestName +';' + Merged/DepTime + ';' + string(Merged/Traveltime) + ';' + Merged/ModeUsed + ';' + Merged/Price;
			parameter<string>  File_fullOD                    := 'OrgName;DestName;DepTime;Traveltime;ModeUsed;Price\n'+AsList(BodyLines_fullOD, '\n')
																			, StorageName = "= '%LocalDataProjDir%/Output/tt_'+fileNameSuffix +'.csv'", StorageType = "str";
			//attribute<string>  BodyLines_fullOD      (Merged) := Merged/OrgName + ';' + Merged/DestName +';' + Merged/DepTime + ';' + string(Merged/Traveltime) + ';' + Merged/ModeUsed;
			//parameter<string>  File_fullOD                    := 'OrgName;DestName;DepTime;Traveltime;ModeUsed\n'+AsList(BodyLines_fullOD, '\n')
			//																, StorageName = "= '%LocalDataProjDir%/Output/tt'+fileNameSuffix +'.csv'", StorageType = "str";
		}
		
		// parameter<string> Export_filename       := Advanced/fileNamePrefix+'tt_'+Export_CombinationTypes+''+fileNameSuffix+'.csv';
		parameter<string> fileNameSuffix :=  
			'_' + Analysis_Date + 
			'_ORG-' + Orgset + 
			'_DEST-' + Dest_str +
			'_'+ Export_CombinationTypes +
			'_Trsf-' + string(ModelParameters/Advanced/Max_overstappen) +
			'_MinCrit-' + AsList(Classifications/FinalParetoAttr/FileNameElement, '_') +
			'_MaxOV-' + string(ModelParameters/MaxOVTime) + 'min'
			;
			
			
	}
	
	
}
