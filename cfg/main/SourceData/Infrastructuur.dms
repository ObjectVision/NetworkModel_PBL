container Infrastructuur
{
	unit<uint32> Read_NS_Stations_SHP
	:	StorageName     = "%NetworkModel_dir%/Infrastructuur/NS_treinstations_20230301.shp"
	,	StorageType     = "gdal.vect"
	,	StorageReadOnly = "True"
	,	URL             = "https://hub.arcgis.com/datasets/822b7e9ad10e476e9092265416ad484d/explore?location=50.006768%2C8.947104%2C6.62"
	{
		attribute<wgs84_base>                geometry;
		attribute<rdc>                       geometry_rd := LatLongWgs842RD(geometry, dpoint)[rdc];
		attribute<string>                    Land;
		attribute<string>                    lang;
		attribute<string>                    Type;
		attribute<string>                    type_code      := (Type == 'knooppuntintercitystation' || Type == 'intercitystation' || Type == 'megastation')
															? 'IC'
															: (Type == 'knooppuntsneltreinstation' || Type == 'sneltreinstation')
																? 'ST'
																: (Type == 'knooppuntstoptreinstation' || Type == 'stoptreinstation')
																	? 'SPR'
																	: 'Anders';
	}
	
	unit<uint32> IC_Stations := select_with_org_rel(Read_NS_Stations_SHP/type_code == 'IC' && Read_NS_Stations_SHP/Land == 'NL')
	{
		attribute<rdc>       geometry := Read_NS_Stations_SHP/geometry_rd[org_rel];
		attribute<string>    code     := Read_NS_Stations_SHP/code[org_rel];
		attribute<string>    label    := Read_NS_Stations_SHP/lang[org_rel];
		
		attribute<uq> uq_rel := rlookup(geometry, uq/geometry);
		unit<uint32> uq := unique(geometry)
		{
			attribute<rdc>     geometry := values;
			attribute<string>  label    := ../label[invert(uq_rel)];
			attribute<string>  code    := ../code[invert(uq_rel)];
		}
	}
	unit<uint32> Trainstations := select_with_org_rel(Read_NS_Stations_SHP/Land == 'NL')
	{
		attribute<rdc>       geometry := Read_NS_Stations_SHP/geometry_rd[org_rel];
		attribute<string>    code     := Read_NS_Stations_SHP/code[org_rel];
		attribute<string>    label    := Read_NS_Stations_SHP/lang[org_rel];
		attribute<bool>      IsIC     := Read_NS_Stations_SHP/type_code[org_rel] == 'IC';
		
		attribute<uq> uq_rel := rlookup(geometry, uq/geometry);
		unit<uint32> uq := unique(geometry)
		{
			attribute<rdc>     geometry := values;
			attribute<string>  label    := ../label[invert(uq_rel)];
			attribute<string>  code    := ../code[invert(uq_rel)];
			
			unit<uint32> doubledpoints := combine(uq, uq)
			, dialogtype = "map", dialogdata = "origin"
			{
				attribute<rdc> origin      := ../geometry[first_rel];
				attribute<rdc> destination := ../geometry[second_rel];
				attribute<m>   dist      := value(dist(origin, destination),m);
				attribute<string> org_code    := ../code[first_rel];
				attribute<string> dst_code    := ../code[second_rel];
			}
			
			unit<uint32> NS_kort_stations := select_with_org_rel(doubledpoints/dist <= 10000[m] && doubledpoints/dist > 0[m] )
			{
				attribute<rdc>    geometry      := org_rel -> origin;
				attribute<m>      dist          := org_rel -> dist;
				attribute<string> code          := org_rel -> org_code;
				
			}
		}
	}
	
	unit<uint32> OV_Fiets_Stations 
	:	StorageName     = "%NetworkModel_dir%/Infrastructuur/ovfietslocaties_20240331.xlsx"
	,	StorageType     = "gdal.vect"
	,	StorageReadOnly = "True"
	{
		attribute<wgs84_base> geometry_ll := point_yx(latitude, longitude, wgs84_base);
		attribute<rdc>        geometry    := LatLongWgs842RD(geometry_ll, dpoint)[rdc];
		
	}
}
