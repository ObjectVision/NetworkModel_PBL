unit<uint32> AddTransfersToScheduledLinks := union_unit(RelevantSelection/ScheduledLinks, CreateTransfersSet/TransfersByStart)
, Descr = "scheduled links + transfer links"
, Using = "RelevantSelection" 
{
	attribute<rdc>                     geometry (arc)  := union_data(., RelevantSelection/ScheduledLinks/geometry, CreateTransfersSet/TransfersByStart/geometry); 
	attribute<Time>                    fromTime        := union_data(., RelevantSelection/ScheduledLinks/depTime, CreateTransfersSet/TransfersByStart/fromTime); 
	attribute<Time>                    toTime          := union_data(., RelevantSelection/ScheduledLinks/arrTime, CreateTransfersSet/TransfersByStart/toTime); 
	attribute<s_f>                     Duration        := union_data(., RelevantSelection/ScheduledLinks/Duration, CreateTransfersSet/TransfersByStart/Duration); 
	attribute<string>                  Label           := union_data(., RelevantSelection/ScheduledLinks/Label, CreateTransfersSet/TransfersByStart/Label), DialogType = "LabelText";
	attribute<Trips>                   Trip_rel        := union_data(., RelevantSelection/ScheduledLinks/Trip_rel, const(0/0,CreateTransfersSet/TransfersByStart));
	attribute<Routes>                  Route_rel       := union_data(., RelevantSelection/ScheduledLinks/Route_rel, const((0/0)[Routes],CreateTransfersSet/TransfersByStart));
	attribute<string>                  RouteName       := union_data(., RelevantSelection/ScheduledLinks/RouteName, const('Walking',CreateTransfersSet/TransfersByStart));
	attribute<string>                  RouteName_short := union_data(., RelevantSelection/ScheduledLinks/RouteName_short, const('Walking',CreateTransfersSet/TransfersByStart));
	attribute<Agencies>                Agency_rel      := union_data(., RelevantSelection/ScheduledLinks/Agency_rel, const(0/0,CreateTransfersSet/TransfersByStart));
	attribute<Modes>                   Mode_rel        := union_data(., RelevantSelection/ScheduledLinks/Mode_rel, const(Modes/V/Walking, CreateTransfersSet/TransfersByStart, Modes));
	attribute<LinkTypes>               LinkType_rel    := union_data(., const(LinkTypes/V/Scheduled,RelevantSelection/ScheduledLinks,LinkTypes), const(LinkTypes/V/Transfer,CreateTransfersSet/TransfersByStart,LinkTypes));
	attribute<Stops>                   FromStop_rel    := union_data(., RelevantSelection/ScheduledLinks/FromStop_rel, CreateTransfersSet/TransfersByStart/FromStop_rel); 
	attribute<Stops>                   ToStop_rel      := union_data(., RelevantSelection/ScheduledLinks/ToStop_rel, CreateTransfersSet/TransfersByStart/ToStop_rel); 
	attribute<rdc>                     fromPoint       := first_point(geometry);
	attribute<rdc>                     toPoint         := last_point(geometry);
	attribute<PT_Places>               FromPT_Place_rel   := rlookup(fromPoint, PT_Places/Values);
	attribute<string>                  FromPT_Place_name  := PT_Places/Stop_name[FromPT_Place_rel];
	attribute<PT_Places>               ToPT_Place_rel     := rlookup(toPoint, PT_Places/Values);
	attribute<string>                  ToPT_Place_name    := PT_Places/Stop_name[ToPT_Place_rel];
	
	unit<uint32> doubledLinks := union_unit(., .)
	, Descr = "in order to get a set of all time-place events"
	{
		attribute<Time>      Moment         := union_data(., fromTime, toTime);
		attribute<rdc>       geometry       := union_data(., fromPoint, toPoint);
		attribute<Stops>     Stop_rel       := rlookup(geometry, Stops/geometry);
		attribute<PT_Places> PT_Place_rel   := rlookup(geometry, PT_Places/geometry);
		
		attribute<c_Time_PT_Places> c_Time_PT_Place_rel := combine_data(c_Time_PT_Places, Moment, PT_Place_rel);
		attribute<c_Time_Stops>     c_Time_Stop_rel     := combine_data(c_Time_Stops, Moment, Stop_rel);
		
	}
	
	unit<uint32> PT_Places := unique(doubledLinks/geometry)
	, Descr = "unique stop locations"
	{
		attribute<Stops>   Stop_rel    := rlookup(geometry, Stops/geometry);
		attribute<string>  Stop_name   := Stops/Name[Stop_rel];
		attribute<string>  Label       := Stop_name;
		attribute<rdc>     geometry    := values;
	}
	
	unit<uint32> uq_TimeEvents    := unique(doubledLinks/Moment);
	unit<uint32> uq_c_Time_Stops  := unique(doubledLinks/c_Time_Stop_rel) //fka UniqueMomentXStop
	{
		attribute<c_Time_Stops> c_Time_Stop_rel    := values;
		attribute<Time>         Time_rel           := value(c_Time_Stop_rel / uint64(#Stops), Time);
		attribute<Stops>        Stop_rel           := value(c_Time_Stop_rel % uint64(#Stops), Stops);
		
		attribute<string>       Label              := Stops/Name[Stop_rel] + ' @ ' + Time/Label[Time_rel];
		attribute<.>            FirstMoment        := min_index(Time_rel, Stop_rel)[Stop_rel];
		attribute<.>            NextMoment         := Stop_rel[add_or_null(id(.),1)] = Stop_rel ? add_or_null(id(.),1) : FirstMoment;
		attribute<bool>         points_unsorted    := Values != sort(Values); // check to verify Values are orderd ascendingly (documented as such in online documentation)
		
		unit<uint32>  MakeWaitingLines := union_unit(., .)
		{
			attribute<rdc>                      geometry     := union_data(., Stops/geometry[Stop_rel], Stops/geometry[Stop_rel]);
			attribute<..>                       sequence_rel := union_data(., id(..), id(..));
			attribute<uint32>                   ordinal      := union_data(., const(0,..,uint32), const(1,..,uint32));
		}
	}
	unit<uint32> WaitingAtStop := uq_c_Time_Stops 
	{
		attribute<rdc>          geometry (arc) := points2sequence(uq_c_Time_Stops/MakeWaitingLines/geometry, uq_c_Time_Stops/MakeWaitingLines/sequence_rel, uq_c_Time_Stops/MakeWaitingLines/ordinal);
		attribute<Stops>        FromStop_rel   := Stop_rel;
		attribute<Stops>        ToStop_rel     := Stop_rel[NextMoment];
		attribute<rdc>          FromPoint      := Stops/geometry[FromStop_rel];
		attribute<rdc>          ToPoint        := Stops/geometry[ToStop_rel];
		attribute<Time>         FromTime       := Time_rel;
		attribute<Time>         ToTime         := Time_rel[NextMoment];
		attribute<s_f>          Duration       := FromTime <= ToTime 
													? float32(sub_or_null(ToTime,FromTime))[s_f] 
													: float32(sub_or_null(ToTime + #Time,FromTime))[s_f]; // duration in seconds
		attribute<string>       Label          := 'Waiting at stop: ' + Stops/Name[FromStop_rel] + ' from ' + Time/Label[FromTime] + ' to ' + Time/Label[ToTime];
	}
	unit<uint32> NodeSet:= unique(doubledLinks/c_Time_PT_Place_rel)
	{
		attribute<c_Time_PT_Places> c_Time_PT_Place_rel := values;
		attribute<Time>             Time_rel            := value(c_Time_PT_Place_rel / uint64(#PT_Places), Time);
		attribute<PT_Places>        PT_Place_rel        := value(c_Time_PT_Place_rel % uint64(#PT_Places), PT_Places);
		attribute<rdc>              geometry            := PT_Places/geometry[PT_Place_rel];
		
		attribute<string>           Label               := PT_Places/label[PT_Place_rel] + ' @ ' + Time/Label[Time_rel];
	}
}

unit<uint32> PublicTransportNet := union_unit(AddTransfersToScheduledLinks, AddTransfersToScheduledLinks/WaitingAtStop)
, Descr = "full public transport network, including transfer links and waiting at stop"
, Using = "RelevantSelection"
{	
	unit<uint32> NodeSet    := AddTransfersToScheduledLinks/Nodeset;
	unit<uint32> PT_Places  := AddTransfersToScheduledLinks/PT_Places;
	unit<uint32> Stops      := RelevantSelection/Stops
	{
		attribute<PT_Places> PT_Place_rel             := rlookup(geometry, PT_Places/geometry);
	}
	unit<uint32> Routes  := RelevantSelection/Routes;
	
	attribute<rdc>                     geometry (arc)                 := union_data(., AddTransfersToScheduledLinks/geometry, AddTransfersToScheduledLinks/WaitingAtStop/geometry);
	attribute<PT_Places>               FromPT_Place_rel               := Stops/PT_Place_rel[FromStop_rel];
	attribute<string>                  FromPT_Place_name              := Stops/name[FromStop_rel];
	attribute<string>                  ToPT_Place_name                := Stops/name[ToStop_rel];
	attribute<PT_Places>               ToPT_Place_rel                 := Stops/PT_Place_rel[ToStop_rel];
	attribute<rdc>                     FromPoint                      := union_data(., AddTransfersToScheduledLinks/FromPoint, AddTransfersToScheduledLinks/WaitingAtStop/FromPoint);
	attribute<rdc>                     ToPoint                        := union_data(., AddTransfersToScheduledLinks/ToPoint, AddTransfersToScheduledLinks/WaitingAtStop/ToPoint);
	attribute<Stops>                   FromStop_rel                   := union_data(., AddTransfersToScheduledLinks/FromStop_rel, AddTransfersToScheduledLinks/WaitingAtStop/FromStop_rel);
	attribute<Stops>                   ToStop_rel                     := union_data(., AddTransfersToScheduledLinks/ToStop_rel, AddTransfersToScheduledLinks/WaitingAtStop/ToStop_rel);
	
	attribute<string>                  Label                          := union_data(., AddTransfersToScheduledLinks/Label, AddTransfersToScheduledLinks/WaitingAtStop/Label);
	attribute<Time>                    FromTime                       := union_data(., AddTransfersToScheduledLinks/fromTime, AddTransfersToScheduledLinks/WaitingAtStop/fromTime);
	attribute<Time>                    ToTime                         := union_data(., AddTransfersToScheduledLinks/toTime, AddTransfersToScheduledLinks/WaitingAtStop/toTime);
	attribute<s_f>                     Duration                       := union_data(., AddTransfersToScheduledLinks/Duration, AddTransfersToScheduledLinks/WaitingAtStop/Duration), IntegrityCheck = "ModelParameters/Add_OV_PriceInformation ? all(RelevantSelection/ScheduledLinks/HasPriceRelation) : TRUE";
	attribute<LinkTypes>               LinkType_rel                   := union_data(., AddTransfersToScheduledLinks/LinkType_rel, const(LinkTypes/V/Waiting_at_Stop, AddTransfersToScheduledLinks/WaitingAtStop, LinkTypes));
	
	attribute<c_Time_PT_Places>        c_fromTime_PT_Place_rel        := combine_data(c_Time_PT_Places, FromTime, FromPT_Place_rel);
	attribute<c_Time_PT_Places>        c_toTime_PT_Place_rel          := combine_data(c_Time_PT_Places, ToTime, ToPT_Place_rel);
	
	attribute<km>                      Length                         := arc_length(geometry, m)[km];
	attribute<Modes>                   Mode_rel                       := union_data(., AddTransfersToScheduledLinks/Mode_rel, const(Modes/V/Waiting, AddTransfersToScheduledLinks/WaitingAtStop, Modes));
	attribute<uint32>                  Trip_rel                       := union_data(., AddTransfersToScheduledLinks/Trip_rel, const(0/0, AddTransfersToScheduledLinks/WaitingAtStop));
	attribute<Agencies>                Agency_rel                     := union_data(., AddTransfersToScheduledLinks/Agency_rel, const(0/0, AddTransfersToScheduledLinks/WaitingAtStop));
	attribute<Routes>                  Route_rel                      := union_data(., AddTransfersToScheduledLinks/Route_rel, const((0/0)[Routes], AddTransfersToScheduledLinks/WaitingAtStop));
	attribute<string>                  RouteName                      := union_data(., AddTransfersToScheduledLinks/RouteName, const('Waiting', AddTransfersToScheduledLinks/WaitingAtStop));
	attribute<string>                  RouteName_short                := union_data(., AddTransfersToScheduledLinks/RouteName_short, const('Waiting', AddTransfersToScheduledLinks/WaitingAtStop));
}
